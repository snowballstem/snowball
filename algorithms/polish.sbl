stringescapes {}

stringdef a' '{U+0105}' 
stringdef e' '{U+0119}' 
stringdef l' '{U+0142}' 
stringdef s' '{U+015b}' 
stringdef c' '{U+0107}' 
stringdef o' '{U+00f3}' 




externals (stem)
integers (pos)


routines (remove_nouns remove_dimunitive remove_adjective_ends remove_verbs_ends remove_adverbs_ends 
	remove_plural_forms remove_general_ends)


define remove_nouns as (
			($(len > 7)
			test ($pos = (len - 5)
			hop pos
			('zacja' or 'zacja' or 'zacji'))
			($pos = (len - 4)
			hop pos
			([tolimit] delete))) or
			($(len > 6)
			test($pos = (len - 4)
			hop pos
			('acja' or 'acji' or 'acj{a'}' or 'tach' or 'anie' or 'enie' or 'eniu' or 'aniu'))
			($pos = (len -4)
			hop pos
			([tolimit] delete))) or
			($(len > 6)
			test($pos = (len - 4)
			hop pos
			('tyka'))
			($pos = (len - 2)
			hop pos
			([tolimit] delete))
			) or
			($(len > 5)
			test ($pos = (len - 3)
			hop pos
			('ach' or 'ami' or 'nia' or 'niu' or 'cia' or 'ciu'))
			($pos = (len -3)
			hop pos
			([tolimit] delete))
			) or
			($(len > 5)
			test ($pos = (len - 3)
			hop pos
			('cji' or 'cja' or 'cj{a'}'))
			($pos = (len - 2)
			hop pos
			([tolimit] delete ))) or
			($(len > 5)
			test($pos = (len - 2)
			hop pos
			('ce' or 'ta'))
			($pos = (len - 2)
			hop pos
			([tolimit] delete))
			)

)

define remove_dimunitive as (
		($(len > 6)
		test ($pos = (len - 5)
		hop pos
		('eczek'  or 'iczek' or 'iszek' or 'aszek' or 'uszek')) 
		($pos = (len - 5)
		hop pos
		([tolimit] delete))) or
		($(len > 6)
		test ($pos = (len - 4)
		hop pos
		('enek' or 'ejek' or 'erek'))
		($pos = (len - 2)
		hop pos
		([tolimit] delete))) or
		($(len > 4)
		test ($pos = (len - 2)
		hop pos
		('ek' or 'ak'))
		($pos = (len -2)
		hop pos
		([tolimit] delete))
		)
)

define remove_adjective_ends as (
				($(len > 7)
				test (('naj')
				$pos = (limit - cursor - 3)
				hop pos
				('sze' or 'szy'))
				(([hop 3] delete)
				$pos = (limit - cursor - 3)
				hop pos
				([tolimit] delete))) or 
				($(len > 7)
				test (('naj')
				$pos = (limit - cursor - 5)
				hop pos
				('szych'))
				(([hop 3] delete)
				$pos = (len - 5)
				hop pos
				([tolimit] delete))) or 
				($(len > 6)
				test ($pos = limit - 4
				hop pos
				('czny'))
				($pos = (len - 4)
				hop pos
				([tolimit] delete))
				) or
				($(len > 5)
				test($pos = (len - 3)
				hop pos
				('owy' or 'owa' or 'owe' or 'ych' or 'ego'))
				($pos = (len - 3)
				hop pos
				([tolimit] delete))) or
				($(len > 5)
				test ($pos = (len - 2)
				hop pos
				('ej'))
				($pos = (len - 2)
				hop pos
				([tolimit] delete)))
)

define remove_verbs_ends as(
			($(len > 5)
			test ($pos = (limit - 3)
			hop pos
			('bym'))
			($pos = (len - 3)
			hop pos
			([tolimit] delete))) or
			($(len > 5)
			test ($pos = (len - 3)
			hop pos
			('esz' or 'asz' or 'cie' or 'e{s'}{c'}' or 'a{s'}c' or '{l'}em' or 'amy' or 'emy'))
			($pos = (len - 3)
			hop pos
			([tolimit] delete))) or
			($(len > 3)
			test ($pos = (len - 2)
			hop pos
			('a{c'}' or 'e{c'}' or 'em' or 'am' or 'a{l'}' or 'i{l'}' or 'i{c'}' or '{a'}c'))
			($pos = (len - 2)
			hop pos
			([tolimit] delete))) or
			($(len > 3)
			test ($pos = (len - 2)
			hop pos
			('aj'))
			($pos = (len -1)
			hop pos
			([tolimit] delete)))
)

define remove_adverbs_ends as (
			($(len > 4)
			test ($pos = (len - 3)
			hop pos
			('nie' or 'wie'))
			($pos = (len - 2)
			hop pos
			([tolimit] delete))) or
			($(len > 4)
			test ($pos = (len - 2)
			hop pos
			('rze'))
			($pos = (len - 2)
			hop pos
			([tolimit] delete)))
)

define remove_plural_forms as (
			($(len > 4)
			test ($pos = (len - 2)
			hop pos
			('{o'}w' or 'om'))
			($pos = (len - 2)
			hop pos
			([tolimit] delete))) or
			($(len > 4)
			test ($pos = (len - 3)
			hop pos
			('ami'))
			($pos = (len -3)
			hop pos
			([tolimit] delete)))
)

define remove_general_ends as (
		($(len > 4)
		test ($pos = (len - 2)
		hop pos
		('ia' or 'ie'))
		($pos = (len - 2)
		hop pos
		([tolimit] delete))) or
		($(len > 4)
		test ($pos = (len - 1)
		hop pos
		('u' or '{a'}' or 'i' or 'a' or '{e'}' or 'y' or '{l'}'))
		($pos = (len - 1)
		hop pos
		([tolimit] delete)))
)

define stem as (
		do remove_nouns
		do remove_dimunitive
		do remove_adjective_ends
		do remove_verbs_ends
		do remove_adverbs_ends
		do remove_plural_forms
		do remove_general_ends
)

