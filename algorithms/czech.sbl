routines (
  R1
  palatalise_e
  palatalise_ecaron
  palatalise_i
  mark_regions
  possessive_suffix
  case_suffix
)

externals ( stem )

integers ( p1 x )

groupings ( v v_or_syllabic_c )

stringescapes {}

stringdef a' '{U+00E1}' // á
stringdef cv '{U+010D}' // č
stringdef dv '{U+010F}' // ď
stringdef e' '{U+00E9}' // é
stringdef ev '{U+011B}' // ě
stringdef i' '{U+00ED}' // í
stringdef nv '{U+0148}' // ň
stringdef o' '{U+00F3}' // ó
stringdef rv '{U+0159}' // ř
stringdef sv '{U+0161}' // š
stringdef tv '{U+0165}' // ť
stringdef u' '{U+00FA}' // ú
stringdef uo '{U+016F}' // ů
stringdef y' '{U+00FD}' // ý
stringdef zv '{U+017E}' // ž

define v 'aeiouy{a'}{ev}{e'}{i'}{o'}{u'}{uo}{y'}'

// Some consonants in Czech can be syllabic - if these occur between two other
// consonants then they act in a vowel-like way and it is helpful to include
// them in the definition of R1.
//
// Some sources also list 'm' and 'n' as syllabic consonants for Czech but they
// seem to be much rarer and including them makes no difference to the results
// of stemming any words in our sample vocabulary list.  Checking on a larger
// vocabulary list (also from wikipedia but with a lower cut-off frequency)
// all but one of the affected words don't seem to actually be Czech words.
define v_or_syllabic_c v + 'lr'

define mark_regions as (

    test (hop 3 setmark x) // Signals f if the input < 3 characters.

    $p1 = limit
    do (
        // A syllabic consonant must occur between two consonants, or be
        // preceded by a consonant and at the end of the word.
        //
        // Instead of literally testing that, we handle the first character
        // specially by only checking if it's a vowel; for subsequent
        // characters we know that the character before is a consonant because
        // otherwise we'd have stopped already.
        //
        // We also don't actually need to check the character after, since
        // if it's a vowel then that vowel means we'd end up at the same
        // position after `gopast non-v` anyway, and if it's the end of the
        // word then there's no non-v after it.
        (v or (next gopast v_or_syllabic_c)) gopast non-v
        setmark p1
        try($p1 < x  $p1 = x)  // at least 3
    )
)

backwardmode (

  define R1 as $p1 <= cursor

  define palatalise_e as (
    [substring] among (
      'c' '{cv}' (<- 'k')
      'z' '{zv}' (<- 'h')
    )
  )

  define palatalise_ecaron as (
    [substring] among (
      '{cv}t' (<- 'ck')
      '{sv}t' (<- 'sk')
    )
  )

  define palatalise_i as (
    [substring] among (
      'c' '{cv}' (<- 'k')
      'z' '{zv}' (<- 'h')
      '{cv}t' (<- 'ck')
      '{sv}t' (<- 'sk')
    )
  )

  define possessive_suffix as (
    [substring] R1 among (
      'ov' '{uo}v'
      (delete)
      'in'
      (
        delete
        try palatalise_i
      )
    )
  )

  define case_suffix as (
    setlimit tomark p1 for ( [substring] ) among (
      'atech'
      'at{uo}m'
      '{a'}ch' '{y'}ch' 'ov{e'}' '{y'}mi'
      'ata' 'aty' 'ama' 'ami' 'ovi'
      'at' '{a'}m' 'us' '{uo}m' '{y'}m' 'mi' 'ou'
      '{e'}ho' '{e'}m' '{e'}mu'
      'u' 'y' '{uo}' 'a' 'o' '{a'}' '{e'}' '{y'}'
      (delete)
      '{ev}' '{ev}tem' '{ev}mi' '{ev}te' '{ev}ti'
      '{ev}m' // e.g. koněm
      (
        delete
        try palatalise_ecaron
      )
      'e' 'ech' 'em' 'emi' 'ete' 'etem'
      (
        delete
        try palatalise_e
      )
      'ec'
      ( // Conflate e.g. obec with obce, obcemi, obci, obcí, obcích
        // The correct linguistic root is the -ec forms, but we map to -c as it
        // seems much harder to reliably map the various -c- forms to -ec.
        test non-v
        delete attach 'c'
        try palatalise_e
      )
      '{tv}' '{tv}mi'
      ( // Conflate e.g. oběť and oběťmi with obětech; hradišť with hradišti.
        delete attach 't'
        try palatalise_ecaron
      )
      'i'
      '{i'}' '{i'}ch' '{i'}ho' '{i'}m' '{i'}mi' '{i'}mu'
      (
        delete
        try palatalise_i
      )
    )
  )
)

define stem as (
  mark_regions // Signals f if the input has < 3 characters.
  backwards (
    do case_suffix
    do possessive_suffix
  )
)

// Ljiljana Dolamic and Jacques Savoy. 2009.
// Indexing and stemming approaches for the Czech language.
// Inf. Process. Manage. 45, 6 (November 2009), 714-720.
// based on Java code by Ljiljana Dolamic:
// http://members.unine.ch/jacques.savoy/clef/CzechStemmerLight.txt
